plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
    id("io.freefair.lombok") version "8.13.1"
}

description = 'OpenAudioMc JUtils'

dependencies {
    implementation 'org.mapdb:mapdb:3.0.8'
    implementation 'net.jpountz.lz4:lz4:1.3'
}

java {
    withSourcesJar()
    withJavadocJar()
}

shadowJar {
    archiveBaseName = 'openaudiomc-jutils'
    archiveClassifier = ''

    // Relocate dependencies to avoid conflicts
    relocate 'com.', 'org.mapdb.thirdparty.com.'
    relocate 'kotlin.', 'org.mapdb.thirdparty.kotlin.'
    relocate 'org.eclipse.', 'org.mapdb.thirdparty.eclipse.'
    relocate 'org.jetbrains.', 'org.mapdb.thirdparty.jetbrains.'

    // Exclude signature files
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

// Make the shadowJar task the default jar
jar.dependsOn shadowJar
jar.enabled = false

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'openaudiomc-jutils'
            version = project.version

            // Use the shadow jar instead of the regular jar
            artifact shadowJar
        }
    }
}
